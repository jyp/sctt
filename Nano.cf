entrypoints Term ;

layout "of" ;

First.  Proj ::= ".1" ;
Second. Proj ::= ".2" ;

separator Tag ",";
T.  Tag      ::= "'" Var;
Br. Branch   ::= Tag "->" Term;

separator Branch ".";
Concl.  Term1 ::=    Var ;
Destr.  Term1 ::=    Var "=" Destr ";" Term1;
Constr. Term1 ::=    Var "=" Constr ";" Term1;
Case.   Term1 ::=    "case" Var "of" "{" [Branch] "}";

coercions Term 1;

Appl.   Destr ::= Var Var;
Proj.   Destr ::= Var Proj;
Cut.    Destr ::= Var ":" Var;

Hyp.    Constr ::= Var;
Lam.    Constr ::= "\\" Var "->" Term;
Pi.     Constr ::= "(" Var ":" Var ")" "->" Term;
Pair.   Constr ::= "(" Var "," Var ")";
Sigma.  Constr ::= "(" Var ":" Var ")" "X" Term;
Tag.    Constr ::= Tag ;
Fin.    Constr ::= "{" [Tag] "}" ;
Univ.   Constr ::= "*" Nat;

position token Var (letter)((letter|digit|'-'|'_'|'\'')*) ;
position token Nat digit+ ;

comment "{-" "-}" ;
comment "--" ;

